programa ejercicio2Practica5
procesos
  proceso juntarFlorPap(ES cantFlor:numero; ES cantPap:numero)
  comenzar
    mientras(HayFlorEnLaEsquina)
      tomarFlor
      cantFlor:=cantFlor + 1 
    mientras(HayPapelEnLaEsquina)
      tomarPapel
      cantPap:= cantPap + 1
  fin
  proceso dejarFlor(ES cantFlor:numero)
  comenzar
    mientras(HayFlorEnLaBolsa)
      depositarFlor
      cantFlor:=cantFlor - 1
  fin
  proceso juntarFlor(ES cantFlor:numero)
  comenzar
    mientras(HayFlorEnLaEsquina)
      tomarFlor
      cantFlor:=cantFlor + 1
  fin
areas
  areaR1 : AreaP(1,1,100,1)
  areaR2 : AreaP(1,2,100,2)
  areaR3 : AreaP(1,3,100,3)
  areaFiscal : AreaP(1,4,1,4)
  areaDeposito : AreaC(50,50,50,50)
robots
  robot recolector
  variables
    cantFlor,cantPap,id,contadorEsquina,avVolver,caVolver:numero
    seguir:boolean
  comenzar
    cantFlor:=0
    cantPap:=0
    contadorEsquina:=0
    derecha
    RecibirMensaje(id,fiscalSupremo)
    repetir 99 
      juntarFlorPap(cantFlor,cantPap)
      mover
      contadorEsquina:= contadorEsquina + 1
      si(contadorEsquina=10)
        avVolver:=PosAv
        caVolver:=PosCa
        BloquearEsquina(50,50)
        Pos(50,50)
        dejarFlor(cantFlor)
        Pos(avVolver,caVolver)
        LiberarEsquina(50,50)
        {envio mensaje de etapa terminada}
        si(id=1)
          EnviarMensaje(V,R2)
          EnviarMensaje(V,R3)
        sino
          si(id=2)
            EnviarMensaje(V,R1)
            EnviarMensaje(V,R3)
          sino
            si(id=3)
              EnviarMensaje(V,R1)
              EnviarMensaje(V,R2)
        contadorEsquina:=0
        RecibirMensaje(seguir,*)
        RecibirMensaje(seguir,*)
    EnviarMensaje(V,fiscalSupremo){finalizaron}  
  fin
  robot fiscal
  variables
    cantFlor:numero
    termino:boolean
  comenzar
    cantFlor:=0
    EnviarMensaje(1,R1)
    EnviarMensaje(2,R2)
    EnviarMensaje(3,R3)
    repetir 3
      RecibirMensaje(termino,*)
    Pos(50,50)
    juntarFlor(cantFlor)
    Informar('total_flores_juntadas',cantFlor)
  fin
variables
  R1 : recolector
  R2 : recolector
  R3 : recolector
  fiscalSupremo : fiscal
comenzar
  AsignarArea(R1,areaR1)
  AsignarArea(R2,areaR2)
  AsignarArea(R3,areaR3)
  AsignarArea(fiscalSupremo,areaFiscal)
  {-----}
  AsignarArea(R1,areaDeposito)
  AsignarArea(R2,areaDeposito)
  AsignarArea(R3,areaDeposito)
  AsignarArea(fiscalSupremo,areaDeposito)
  {-----}
  Iniciar(R1,1,1)
  Iniciar(R2,1,2)
  Iniciar(R3,1,3)
  Iniciar(fiscalSupremo,1,4)
fin